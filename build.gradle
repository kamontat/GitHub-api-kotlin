group 'com.kamontat.github'
version '0.0.1-alpha.2'

buildscript {
    ext.kotlin_version = '1.1.2'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    
    compile 'com.beust:klaxon:0.30'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

//artifacts {
//    archives sourcesJar
//    archives javadocJar
//}

// deploy to jcenter
// must have file call `bintray.sh`
//      inside have
//          export BINTRAY_USER="<NAME>"
//          export BINTRAY_KEY="<API_KEY>"
// run file in commandline with `source ./bintray.sh` command

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'github-api-kotlin'
        name = 'core-api'
        websiteUrl = 'https://github.com/kamontat/GitHub-api-kotlin'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/kamontat/GitHub-api-kotlin.git'
        publicDownloadNumbers = true

        version {
            name = '0.0.1-alpha.2'
            desc = 'test version'
            released = new Date()
            vcsTag = '0.0.1-alpha.2'
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "BSD 3-Clause"
            url "https://raw.githubusercontent.com/kamontat/GitHub-api-kotlin/master/LICENSE"
        }
    }
    developers {
        developer {
            name "Kamontat Chantrachirathumrong"
            email "kamontat_c@hotmail.com"
        }
    }
    scm {
        connection "scm:git:git://github.com/kamontat/GitHub-api-kotlin.git"
        developerConnection "scm:git:ssh://github.com:kamontat/GitHub-api-kotlin.git"
        url "https://github.com/kamontat/GitHub-api-kotlin/tree/master"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            // artifact javadocJar
            groupId 'com.kamontat.github'
            artifactId 'github-api-kotlin'
            version '0.0.1-alpha.2'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'github api implemented to kotlin')
                root.appendNode('name', 'Github-api-kotlin')
                root.appendNode('url', 'https://github.com/kamontat/GitHub-api-kotlin')
                root.children().last() + pomConfig
            }
        }
    }
}